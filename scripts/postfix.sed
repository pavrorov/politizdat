#!/bin/sed -f

# Читаем весь абзац до конца
: read
$ b main;
/\n$/ b main;
N; b read;

: main
# Комментарии в середине строки придётся удалить
/^[[:space:]]*%/ { P; D }
s/\([^\\]\)%[^\n]*\n/\1/g;

# Удаляем последний перевод строки
s/[[:space:]]\+$//;

# Восстанавливаем ключи ссылок на литературу
s/\(\\[[:upper:][:lower:]]*cite[[:lower:]]*\)\([[:space:]]*\[[^]]*\]\)\?\([[:space:]]\+\)\?{\([^}]*\)-\+\([^}]*\)}/\1\2\3{\4-\5}/g;
s/\(\\[[:upper:][:lower:]]*cite[[:lower:]]*\)\([[:space:]]*\[[^]]*\]\)\?\([[:space:]]\+\)\?{\([^}]*\)\.~\([^}]*\)}/\1\2\3{\4.\5}/g;
s/\(\\[[:upper:][:lower:]]*cite[[:lower:]]*\)\([[:space:]]*\[[^]]*\]\)\?\([[:space:]]\+\)\?{\([^}]*\)\\,\([^}]*\)}/\1\2\3{\4\5}/g;
s/\(\\[[:upper:][:lower:]]*cite[[:lower:]]*\)\([[:space:]]*\[[^]]*\]\)\?\([[:space:]]\+\)\?{\([^}]*\)\\[[:lower:]]*hyp\([[:space:]]\+\|{}\)\([^}]*\)}/\1\2\3{\4-\6}/g;
s/\(\\[[:upper:][:lower:]]*cite[[:lower:]]*\)\([[:space:]]*\[[^]]*\]\)\?\([[:space:]]\+\)\?{\([^}]*\)\\mbox{\([^}]*\)}\([^}]*\)}/\1\2\3{\4\5\6}/g;
s/\(\\[[:upper:][:lower:]]*cite[[:lower:]]*\)\([[:space:]]*\[[^]]*\]\)\?\([[:space:]]\+\)\?{\([^}]*\)--\+\([^}]*\)}/\1\2\3{\4-\5}/g;

# Восстанавливаем метки
s/\(\\\(label\|startlabel\|ref\|pageref\|rangeref[[:lower:]]*\)\)\([[:space:]]*\[[^]]*\]\)\?\([[:space:]]\+\)\?{\([^}]*\)\\hyp[[:space:]]*\([^}]*\)}/\1\3\4{\5-\6}/g;

# Восстанавливаем ошибочно связанные команды
s/\(\\[[:lower:]]\+=[0-9]\+\)~/\1\n/g;

# Восстанавливаем тире после популярных названий
s/\(Интернационал\([ау]\|ом\|ах\)\)--\([[:upper:]][[:lower:]]\+\)/\1~--- \3/g;

# Меняем пометки на нуовую версию (заглавне буквы в отдельных
# предложениях и строчные буквы в скобках)
s/\(\\[[:upper:][:lower:]]*cite\)\([[:space:]]*\[[^]]*\]\)\?\([[:space:]]\+\)\?{\([^}]*\)}\([[:space:]]\+\)\?{\(\\myem\\amk\)}/\1\2\3{\4}\5{\\Myem\\amk}/g;
s/\(\\[[:upper:][:lower:]]*cite\)\([[:space:]]*\[[^]]*\]\)\?\([[:space:]]\+\)\?{\([^}]*\)}\([[:space:]]\+\)\?{\(\\ourem\\amk\)}/\1\2\3{\4}\5{\\Ourem\\amk}/g;
s/(\\Myem\\amk)/(\\myem\\amk)/g; s/(\\Ourem\\amk)/(\\ourem\\amk)/g;

# Добавляем пустую строку после абзаца (кроме последней строки)
$! s/^.*$/&\n/;
